buildscript {
	ext.commonsIoVersion = '2.6'
	ext.guavaVersion = '23.4-jre'
	ext.hamcrestVersion = '1.3'
	ext.kotlinVersion = '1.1.60'
	ext.logbackVersion = '1.2.3'
	ext.lombokVersion = '1.16.18'
	ext.okhttpVersion = '3.9.1'
	ext.slf4jVersion = '1.7.25'
	ext.testngVersion = '6.12'
	
	repositories {
		mavenLocal()
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		// kotlin
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.15"
		
		// licenser
		classpath "gradle.plugin.net.minecrell:licenser:0.3"
		
		// code coverage
		classpath "com.palantir:jacoco-coverage:0.4.0"
		classpath "com.github.ksoichiro:gradle-console-reporter:0.5.0"
		
		// version check
		classpath "com.github.ben-manes:gradle-versions-plugin:0.17.0"
	}
}

allprojects {
	group 'msrd0.matrix'
	version "$matrix_client_version"
	
	apply plugin: 'java'
	apply plugin: 'com.github.ben-manes.versions'
	
	apply plugin: 'net.minecrell.licenser'
	license {
		header = project.file('HEADER.txt')
		include '**/*.kt', '**/*.java'
		ignoreFailures = true
	}
	
	repositories {
		mavenLocal()
		jcenter()
		maven { url "https://maven.devpi.de/repository/default/" }
	}
	
	dependencies {
		// java lombok annotations
		compileOnly "org.projectlombok:lombok:$lombokVersion"
		
		// logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"
		
		// test stuff
		testCompile "org.testng:testng:$testngVersion"
		testCompile "org.hamcrest:hamcrest-integration:$hamcrestVersion"
		testCompile "ch.qos.logback:logback-classic:$logbackVersion"
	}
	
	sourceCompatibility = "1.8"
	
	test {
		useTestNG()
		testLogging.showStandardStreams = true
	}
	
	task sourcesJar(type : Jar) {
		from sourceSets.main.allSource
	}
}

configure(allprojects - project('matrix-client-cli')) {
	
	apply plugin: 'kotlin'
	apply plugin: "com.palantir.jacoco-full-report"
	apply plugin: "com.github.ksoichiro.console.reporter"
	
	dependencies {
		// stdlib
		compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
		compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
	}
	
	sourceSets {
		main {
			kotlin.srcDirs = ['src/']
			java.srcDirs = ['src/']
			resources.srcDirs = ['res/']
		}
		test {
			kotlin.srcDirs = ['test/src/']
			java.srcDirs = []
			resources.srcDirs = ['test/res/']
		}
	}
	
	compileKotlin {
		kotlinOptions.jvmTarget = "1.8"
	}
	compileTestKotlin {
		kotlinOptions.jvmTarget = "1.8"
	}
	
	apply plugin: "org.jetbrains.dokka"
	
	dokka {
		outputFormat = "javadoc"
		outputDirectory = "$buildDir/javadoc"
		
		jdkVersion = 8
		impliedPlatforms = ["JVM"]
		
		linkMapping {
			dir = "src/"
			url = "https://gitlab.com/mextrix/matrix-client/tree/master/src"
			suffix = "#L"
		}
	}
	build {
		dependsOn "dokka"
	}
	
	apply plugin: 'maven-publish'
	
	publishing {
		publications {
			mavenKotlin(MavenPublication) {
				from components.java
				
				artifact sourcesJar {
					classifier "sources"
				}
				
				pom.withXml {
					asNode()
						.appendNode('licenses')
							.appendNode('license')
								.appendNode('name', "GNU General Public License, Version 3")
									.parent()
								.appendNode('url', "http://gnu.org/licenses/gpl-3.0")
									.parent()
								.appendNode('distribution', "repo")
									.parent()
								.appendNode('comments', "A copyleft license for free open source software")
				}
			}
		}
		
		repositories {
			maven {
				url "https://maven.devpi.de/repository/default/"
				credentials(PasswordCredentials) {
					username "gitlab-ci"
					password System.getenv("ARTIFACTORY_PASSWORD")
				}
			}
		}
	}
	
}

dependencies {
	
	// apache commons
	compile "commons-io:commons-io:$commonsIoVersion"
	
	// http lib
	compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
	
	// json lib
	compile "com.beust:klaxon:0.32-SNAPSHOT"
	
	// olm java
	compile "msrd0.matrix:matrix-olm-java:2.+"
}
