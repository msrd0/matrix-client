group 'msrd0.matrix'
version "$matrix_client_version"

buildscript {
	ext.kotlin_version = '1.1.+'
	
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:+"
	}
}

apply plugin: 'kotlin'

allprojects {
	repositories {
		mavenLocal()
		jcenter()
	}
}

dependencies {
	// stdlib
	compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	
	// http lib
	compile "org.apache.cxf:cxf-rt-rs-client:3.+"
	
	// json lib
	compile "com.beust:klaxon:0.30"
	
	// logging
	compile "org.slf4j:slf4j-api:1.7.+"
	
	// test stuff
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
	testCompile "org.hamcrest:hamcrest-integration:1.+"
	testCompile "ch.qos.logback:logback-classic:1.+"
}

sourceSets {
	main {
		kotlin.srcDirs = ['src/']
		java.srcDirs = []
		resources.srcDirs = ['res/']
	}
	test {
		kotlin.srcDirs = ['test/']
		java.srcDirs = []
		resources.srcDirs = []
	}
}

compileKotlin {
	kotlinOptions.jvmTarget = "1.6"
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.6"
}


apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

publishing {
	publications {
		mavenKotlin(MavenPublication) {
			from components.java
			pom.withXml {
				asNode()
					.appendNode('licenses')
						.appendNode('license')
							.appendNode('name', "GNU General Public License, Version 3")
								.parent()
							.appendNode('url', "http://gnu.org/licenses/gpl-3.0")
								.parent()
							.appendNode('distribution', "repo")
								.parent()
							.appendNode('comments', "A copyleft license for free open source software")
			}
		}
	}
}

artifactory {
	contextUrl = 'https://msrd0.duckdns.org/artifactory'
	resolve {
		repository {
			repoKey = 'gradle'
		}
	}
	publish {
		repository {
			repoKey = 'local'
			username = 'gitlab-ci'
			password = System.getenv("ARTIFACTORY_PASSWORD")
			maven = true
		}
		defaults {
			publications('mavenKotlin')
		}
	}
}

artifactoryPublish {
	dependsOn jar
}
